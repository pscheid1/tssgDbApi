import groovy.io.FileType
import groovy.xml.MarkupBuilder
import groovy.json.JsonOutput
import org.apache.tools.ant.taskdefs.condition.Os

import static groovyx.net.http.HttpBuilder.configure
//import static groovyx.net.http.ContentTypes.JSON
import groovyx.net.http.HttpBuilder
//import static groovy.json.JsonOutput.prettyPrint

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        //classpath group: 'io.github.http-builder-ng', name: 'http-builder-ng-core', version: '0.18.0'
        classpath group: 'io.github.http-builder-ng', name: 'http-builder-ng-core', version: '1.0.3'
    }
}

plugins {
    id "idea"
    id "groovy"
    id "com.energizedwork.webdriver-binaries" version "1.4"
    id "com.energizedwork.idea-base" version "1.4"
}

ext {
    // The drivers we want to use
    //drivers = ["firefox", "chrome"]
    //drivers = ["firefox","firefoxLinux"]
    drivers = ["chrome"]

    ext {
        groovyVersion = '2.4.12'
        gebVersion = '2.2'
        seleniumVersion = '3.6.0'
        chromeDriverVersion = '2.32'
        geckoDriverVersion = '0.18.0'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // If using Spock, need to depend on geb-spock
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude group: "org.codehaus.groovy"
    }
    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"

    // If using JUnit, need to depend on geb-junit (3 or 4)
    testCompile "org.gebish:geb-junit4:$gebVersion"

    // Drivers
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"

}

webdriverBinaries {
    chromedriver chromeDriverVersion
    geckodriver geckoDriverVersion
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        reports {
            html.destination = reporting.file("$name/tests")
            junitXml.destination = file("$buildDir/test-results/$name")
        }

        outputs.upToDateWhen { false }  // Always run tests

        systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
        systemProperty "geb.env", driver

        // If you wanted to set the baseUrl in your buildâ€¦
        // systemProperty "geb.build.baseUrl", "http://myapp.com"
        systemProperties System.properties

    }
}

test {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = false
    systemProperties System.properties

}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.memoryMaximumSize = '256m'
}

def xrayTestExecutionsJson

// Initialize and accumulate the test case execution results per test key
tasks.withType(Test) {
    maxHeapSize = "1g"
    jvmArgs '-XX:MaxMetaspaceSize=128m'
    testLogging {
        exceptionFormat = 'full'
    }

    // Setup variables to use for getting Xray to import test results
    def testKeyMatcher
    def testKey
    def testStatus
    def xrayTestExecution =  [
            testKey: '',
            //start: '',
            //finish: '',
            comment: '',
            status: ''
    ]
    def xrayTestExecutions = [
            info: [
                    summary: 'TE: Execution of automated tests for release v3.2.1',
                    description: 'This execution is automatically created when importing execution results from an external source',
                    version: '3.2.1',
                    //user: System.properties['jira.test.executor'],
                    revision: '3.2.0',
                    //startDate: '',
                    //finishDate: '',
                    //testPlanKey: 'SPIN-4',
                    testEnvironments: [System.properties['geb.build.baseUrl']]
            ],
            tests: [
            ]
    ]

    // Pass system properties to the Geb JVM from the Gradle JVM
    systemProperties['browser.location'] = System.properties['browser.location']
    systemProperties['geb.build.baseUrl'] = System.properties['geb.build.baseUrl']

    onOutput { desc, event ->
        println "event.message: ${event.message}"
        testKeyMatcher = event.message =~ /TWS-\d+/
        if (testKeyMatcher) {
            testKey = testKeyMatcher.group()
            //logger.quiet "Test Event Message: ${testKeyMatcher.group()}: ${event.message}"
        }
    }

    // Collect the test key, name, and status after each test run
    afterTest { desc, result ->
        logger.quiet "Executed test ${testKey}: ${desc.name} [${desc.className}] with result: ${result.resultType}"
        xrayTestExecution.testKey = testKey
        xrayTestExecution.comment = "${desc.name} [${desc.className}]"
        // Translate test results into Xray test statuses
        switch (result.resultType) {
            case 'SUCCESS':
                xrayTestExecution.status = 'PASS'
                break
            case 'FAILURE':
                xrayTestExecution.status = 'FAIL'
                //xrayTestExecution.comment += "\nEvent Message: Start ${event.message}"
                break
            default:
                xrayTestExecution.status = 'TODO'
                break
        }
        xrayTestExecutions.tests << xrayTestExecution
        xrayTestExecution =  [
                testKey: '',
                //start: '',
                //finish: '',
                comment: '',
                status: ''
        ]
        if (result.resultType == 'FAILURE') {
            logger.quiet "Event Message: Start ${event.message}"
        }
        xrayTestExecutionsJson = JsonOutput.toJson(xrayTestExecutions)
    }
}

// Call xray to import the test execution results
task xrayImport {
    description 'Call xray to import the test execution results.  Requires jira.auth property.'
    doLast {
        println JsonOutput.prettyPrint(xrayTestExecutionsJson)
        def username = System.properties['jira.username']
        def password = System.properties['jira.password']
        def creds = "$username:$password".bytes.encodeBase64()

        def xrayResult = HttpBuilder.configure {
            request.uri = 'http://jira.web.technologynursery.org'
            //request.headers['Authorization'] = 'Basic ' + System.properties['jira.auth']
            request.headers['Authorization'] = "Basic $creds"
            request.contentType = 'application/json'
        }.post {
            request.uri.path = '/rest/raven/1.0/import/execution'
            request.body = xrayTestExecutionsJson
        }
        println "Result = " + xrayResult
    }
}

//xrayImport.mustRunAfter firefoxTest
xrayImport.mustRunAfter chromeTest

task reportSnaps << {
    def listUnsorted = []
    def list = []
    def unixPath

    def dir = new File("build/reports")
    dir.eachFileRecurse (FileType.FILES) { file ->
        unixPath = file.path.replace("\\","/")
        if (unixPath.contains("/geb/")) {
            listUnsorted << file
        }
    }

    list = listUnsorted.sort()
    list.each {
        println it.path.replace("\\","/")
    }

    generateScreenshotsReportsPage(list)
}

//reportSnaps.mustRunAfter firefoxTest
reportSnaps.mustRunAfter chromeTest

/**
 * Generate an html page that contains links to the Report Screnshots
 * and other report artifact files.
 *
 * @param list - A list of report artifact File objects.
 */
void generateScreenshotsReportsPage(list) {

    def reportArtifactsHtmlFile = new File('build/reports/reportArtifacts.html')
    def writer = new StringWriter()
    def html = new MarkupBuilder(writer)
    def now = new Date()
    html.html {
        head {
            title "Report Screen Shots"
        }
        body(id: "main") {
            h2 id: "report-files",  "Report Artifact Files"
            ul {
                list.each { reportArtifactFile ->
                    li {
                        // Build link and extract the filename from the path
                        a( href: removeFirstTwoDirectoriesFromPath(reportArtifactFile.path.replace("\\","/")),
                                removeFirstTwoDirectoriesFromPath(reportArtifactFile.path.replace("\\","/")) )    // display relative path
                        //reportArtifactFile.path.split(File.separator)[-1] )           // display just filename
                    }
                }
            }
            h3 "Report Generated Time"
            p now
        }
    }
    reportArtifactsHtmlFile.write writer.toString()
}

/**
 * Removes the first two directories from the path
 *
 * @parm reportFile -The reportFile
 */
String removeFirstTwoDirectoriesFromPath(String reportArtifactFilePath) {
    String reportFileNewPath = ""
    reportArtifactFilePath.split('/')[2..-1].each {
        // Rebuild the path
        reportFileNewPath += ( it + '/' )
    }
    // Return path with last character from path removed
    reportFileNewPath[0..-2]
}

//apply from: "gradle/ci.gradle"
